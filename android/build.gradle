buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.8.0"
  }
}

def reactNativeArchitectures() {
  def value = rootProject.getProperties().get("reactNativeArchitectures")
  return value ? value.split(",") : ["armeabi-v7a", "x86", "x86_64", "arm64-v8a"]
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: 'org.jetbrains.kotlin.android'
apply from: '../nitrogen/generated/android/NitroList+autolinking.gradle'
apply from: "./fix-prefab.gradle"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["NitroList_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["NitroList_" + name]).toInteger()
}

android {
  namespace "com.nitrolist"

  ndkVersion getExtOrDefault("ndkVersion")
  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()

    externalNativeBuild {
      cmake {
        cppFlags "-frtti -fexceptions -Wall -Wextra -fstack-protector-all"
        arguments "-DANDROID_STL=c++_shared", "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"
        abiFilters (*reactNativeArchitectures())

        buildTypes {
          debug {
            cppFlags "-O1 -g"
          }
          release {
            cppFlags "-O2"
          }
        }
      }
    }
  }

  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }

  packagingOptions {
    excludes = [
        "META-INF",
        "META-INF/**",
        "**/libc++_shared.so",
        "**/libfbjni.so",
        "**/libjsi.so",
        "**/libfolly_json.so",
        "**/libfolly_runtime.so",
        "**/libglog.so",
        "**/libhermes.so",
        "**/libhermes-executor-debug.so",
        "**/libhermes_executor.so",
        "**/libreactnative.so",
        "**/libreactnativejni.so",
        "**/libturbomodulejsijni.so",
        "**/libreact_nativemodule_core.so",
        "**/libjscexecutor.so"
    ]
  }

  buildFeatures {
    buildConfig true
    prefab true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
//    main {
//      if (isNewArchitectureEnabled()) {
//        java.srcDirs += [
//          // React Codegen files
//          "${project.buildDir}/generated/source/codegen/java"
//        ]
//      }
//    }
      main {
          // ✅ Nitro generated Kotlin (CORRECT PATH)
          kotlin.srcDirs += [
                  "$projectDir/../nitrogen/generated/android/kotlin"
          ]

          if (isNewArchitectureEnabled()) {
              java.srcDirs += [
                      "${project.buildDir}/generated/source/codegen/java"
              ]
          }
      }
  }
}

repositories {
  mavenCentral()
  google()
}


dependencies {
  // React Native
  implementation "com.facebook.react:react-native:+"

  // Nitro JSI
  implementation project(":react-native-nitro-modules")

  // ✅ REQUIRED: RecyclerView
  implementation "androidx.recyclerview:recyclerview:1.3.2"
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "NitroList"
    codegenJavaPackageName = "com.nitrolist"
  }
}